
&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ДанныеКартинки = ТекущийОбъект.Картинка.Получить();
	Если ДанныеКартинки <> Неопределено Тогда
		АдресКартинки = ПоместитьВоВременноеХранилище(ДанныеКартинки, УникальныйИдентификатор);
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если ЗначениеЗаполнено(АдресКартинки) Тогда
		ТекущийОбъект.Картинка = Новый ХранилищеЗначения(ПолучитьИзВременногоХранилища(АдресКартинки));
	Иначе
		ТекущийОбъект.Картинка = Неопределено;
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Асинх Процедура КартинкаНажатие(Элемент, СтандартнаяОбработка)
	
	ПараметрыДиалога = Новый ПараметрыДиалогаПомещенияФайлов;
    ПараметрыДиалога.Заголовок = "Выберите картинку для вопроса";
   
    ОписаниеФайла = Ждать ПоместитьФайлНаСерверАсинх(, , , ПараметрыДиалога, УникальныйИдентификатор);
	Если ОписаниеФайла = Неопределено Тогда
		Возврат;
	КонецЕсли; 
	
	АдресКартинки = ОписаниеФайла.Адрес;
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьКартинку(Команда)
	АдресКартинки = "";
КонецПроцедуры
