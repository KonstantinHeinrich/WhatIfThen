
&НаКлиенте
Перем Миганий;

&НаКлиенте
Перем ДанныеВолчка;

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОбновитьЗаголовок();
	
	ДобавляемыеРеквзиты = Новый Массив;
	Для Сч = 1 По 13 Цикл
		ДобавляемыеРеквзиты.Добавить(Новый РеквизитФормы("Картинка" + Сч, Новый ОписаниеТипов("Строка")));
	КонецЦикла;
	ИзменитьРеквизиты(ДобавляемыеРеквзиты);
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	Вопросы.Код КАК Номер,
	|	Вопросы.Наименование КАК Заголовок,
	|	Вопросы.Автор КАК Автор,
	|	Вопросы.Картинка КАК Картинка,
	|	ВЫБОР
	|		КОГДА Вопросы.Блиц
	|			ТОГДА ""Блиц ""
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК Блиц
	|ИЗ
	|	Справочник.Вопросы КАК Вопросы
	|ГДЕ
	|	НЕ Вопросы.ПометкаУдаления
	|
	|УПОРЯДОЧИТЬ ПО
	|	Номер");
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Описание = СтрШаблон("Вопрос № %1 %2%3", Выборка.Номер, Выборка.Блиц, Выборка.Автор);
		СписокВопросов.Добавить(Выборка.Номер, Описание);
		
		ДанныеКартинки = Выборка.Картинка.Получить();
		Если ДанныеКартинки = Неопределено Тогда
			Продолжить;
		КонецЕсли; 
		
		ИмяКартинки = "Картинка" + Выборка.Номер;
		ЭтотОбъект[ИмяКартинки] = ПоместитьВоВременноеХранилище(ДанныеКартинки);
		
		ИмяГруппы = "ГруппаВопрос" + Выборка.Номер;
		НоваяГруппа = Элементы.Добавить(ИмяГруппы, Тип("ГруппаФормы"), Элементы.ГруппаВопросы);
		НоваяГруппа.Вид = ВидГруппыФормы.Страница;
		НоваяГруппа.Заголовок = СтрШаблон("%1. %2", Выборка.Номер, Выборка.Заголовок);
		
		НовоеПоле = Элементы.Добавить(ИмяКартинки, Тип("ПолеФормы"), НоваяГруппа);
		НовоеПоле.Вид = ВидПоляФормы.ПолеКартинки;
		НовоеПоле.ПутьКДанным = ИмяКартинки;
		НовоеПоле.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
		НовоеПоле.Рамка = Новый Рамка(ТипРамкиЭлементаУправления.БезРамки);
		НовоеПоле.РазмерКартинки = РазмерКартинки.Пропорционально;
		НовоеПоле.АвтоМаксимальнаяВысота = Ложь;
		НовоеПоле.РастягиватьПоВертикали = Ложь;
		НовоеПоле.Высота = 25;
		
	КонецЦикла; 
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ДекорацияВыберитеВопросНажатие(Элемент)
	ОткрытьФорму("Справочник.Вопросы.ФормаСписка");
КонецПроцедуры
 
#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОчкоЗнатокам(Команда)
	ИзменитьСчет("СчетЗнатоки");
КонецПроцедуры

&НаКлиенте
Процедура ОчкоТелезрителям(Команда)
	ИзменитьСчет("СчетТелезрители");
КонецПроцедуры

&НаКлиенте
Процедура МинусОчкоЗнатокам(Команда)
	ИзменитьСчет("СчетЗнатоки", -1);
КонецПроцедуры

&НаКлиенте
Процедура МинусОчкоТелезрителям(Команда)
	ИзменитьСчет("СчетТелезрители", -1);
КонецПроцедуры

&НаКлиенте
Процедура ОбнулитьОчкиЗнатокам(Команда)
	ИзменитьСчет("СчетЗнатоки", -СчетЗнатоки);
КонецПроцедуры

&НаКлиенте
Процедура ОбнулитьОчкиТелезрителям(Команда)
	ИзменитьСчет("СчетТелезрители", -СчетТелезрители);
КонецПроцедуры

&НаКлиенте
Процедура МинутаПошла(Команда)
	ПерезапуститьВремя(60);    
КонецПроцедуры

&НаКлиенте
Процедура Блиц(Команда)
	ПерезапуститьВремя(20);
КонецПроцедуры

&НаКлиенте
Процедура ПускВолчка(Команда)
	
	ТекущиеВопросы = Новый Массив;
	Для каждого Вопрос Из СписокВопросов Цикл
		Если Не Вопрос.Пометка Тогда
			ТекущиеВопросы.Добавить(Вопрос.Значение);
		КонецЕсли; 
	КонецЦикла;
	
	Если ТекущиеВопросы.Количество() = 0 Тогда
		Возврат;
	КонецЕсли; 
	
	ГСЧ = Новый ГенераторСлучайныхЧисел;
	Пока Истина Цикл
		Волчок = ГСЧ.СлучайноеЧисло(1, 13);
		Индекс = ТекущиеВопросы.Найти(Волчок);
		Если Индекс <> Неопределено Тогда
			Прервать;
		КонецЕсли; 
	КонецЦикла; 
	
	ДанныеВолчка = Новый Структура;
	ДанныеВолчка.Вставить("Вопросы", ТекущиеВопросы);
	ДанныеВолчка.Вставить("ТекущийИндекс", Индекс);
	ДанныеВолчка.Вставить("Проход", 1);
	ПодключитьОбработчикОжидания("ВращениеВолчка", 0.3, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьСчет(Команда)
	Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаСчет;
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьВолчок(Команда)
	Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаВолчок;
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьКартинки(Команда)
	Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаКартинки;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбновитьЗаголовок()
	
	НомерВопроса = СчетЗнатоки + СчетТелезрители + 1;
	Заголовок = Константы.ЗаголовокИгры.Получить() + ". Вопрос №" + НомерВопроса;
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьСчет(Сторона, Дельта = 1)
	
	ЭтотОбъект[Сторона] = ЭтотОбъект[Сторона] + Дельта;
	Элементы.ПоказатьСчет.Заголовок = СтрШаблон("%1:%2", СчетЗнатоки, СчетТелезрители); 
	ОбновитьЗаголовок();
	
КонецПроцедуры

&НаКлиенте
Процедура ПерезапуститьВремя(Секунд)
	
	Если Время = 0 Тогда
		ВремяОкончания = ТекущаяДата() + Секунд;
		ОбновитьВремя();
		ПодключитьОбработчикОжидания("ОбновитьВремя", 1);
	Иначе
		Время = 0;
		ОтключитьОбработчикОжидания("ОбновитьВремя");
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОбновитьВремя()
	
	Время = ВремяОкончания - ТекущаяДата();
	Если Время < 1 Тогда
		ОтключитьОбработчикОжидания("ОбновитьВремя");
		Миганий = 0;
		ПомигатьВременем();
	КонецЕсли; 
	
КонецПроцедуры
 
&НаКлиенте
Процедура ПомигатьВременем()
	
	Если Элементы.Время.ЦветТекста = WebЦвета.Черный Тогда
		Элементы.Время.ЦветТекста = WebЦвета.Красный;
	Иначе
		Элементы.Время.ЦветТекста = WebЦвета.Черный;
	КонецЕсли; 
	
	Миганий = Миганий + 1;
	Если Миганий < 10 Тогда
		ПодключитьОбработчикОжидания("ПомигатьВременем", 0.3, Истина);
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ПомигатьВолчком()
	
	Если Элементы.Волчок.ЦветТекста = WebЦвета.Черный Тогда
		Элементы.Волчок.ЦветТекста = WebЦвета.Красный;
	Иначе
		Элементы.Волчок.ЦветТекста = WebЦвета.Черный;
	КонецЕсли; 
	
	Миганий = Миганий + 1;
	Если Миганий < 10 Тогда
		ПодключитьОбработчикОжидания("ПомигатьВолчком", 0.3, Истина);
	Иначе
		Элементы.Волчок.ЦветТекста = WebЦвета.Черный;
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ВращениеВолчка()
	
	ДанныеВолчка.ТекущийИндекс = ДанныеВолчка.ТекущийИндекс + 1;
	Если ДанныеВолчка.ТекущийИндекс = ДанныеВолчка.Вопросы.Количество() Тогда
		ДанныеВолчка.ТекущийИндекс = 0;
	КонецЕсли; 
	
	Волчок = ДанныеВолчка.Вопросы[ДанныеВолчка.ТекущийИндекс];
	
	Если ДанныеВолчка.Проход <= 6 Тогда
		Интервал = 0.3;
	ИначеЕсли ДанныеВолчка.Проход <= 11 Тогда
		Интервал = 0.4;
	ИначеЕсли ДанныеВолчка.Проход <= 15 Тогда
		Интервал = 0.5;
	ИначеЕсли ДанныеВолчка.Проход <= 18 Тогда
		Интервал = 0.6;
	ИначеЕсли ДанныеВолчка.Проход <= 21 Тогда
		Интервал = 0.7;
	ИначеЕсли ДанныеВолчка.Проход <= 23 Тогда
		Интервал = 0.8;
	КонецЕсли;
	
	Если ДанныеВолчка.Проход = 24 Тогда
		Миганий = 0;
		ПомигатьВолчком();
		СписокВопросов.НайтиПоЗначению(Волчок).Пометка = Истина;
	Иначе
		ДанныеВолчка.Проход = ДанныеВолчка.Проход + 1;
		ПодключитьОбработчикОжидания("ВращениеВолчка", Интервал, Истина);
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти
